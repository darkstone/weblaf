plugins {
    id 'us.kirchmeier.capsule'  version '1.0-rc1'
    id 'de.gliderpilot.jnlp'    version '0.2.1'
}

ext.description = "Demo application for WEB L&F"
ext.demoMainClassName = 'com.alee.examples.WebLookAndFeelDemo'

apply plugin: 'application'
apply plugin: 'de.gliderpilot.jnlp'

dependencies {
	compile project(':modules:ui')
}

jar {
    from sourceSets.main.allSource
}

task createDemoAppFatCapsule(type: FatCapsule) {
    archiveName = 'weblafdemo.jar'
    setDescription(
        '\n\tCreates a fully executable jar (called a capsule) with \n' +
        '\tall dependencies included in a single jar. Run the jar with java, \n' +
        '\tor in linux/osx by making it executable, and just clicking on it')
    capsuleManifest {
        applicationClass demoMainClassName
        reallyExecutable
        minJavaVersion = "${javacTarget}"
    }
}

jnlp {
    useVersions = true
    withXml {
        information {
            title ext.description
            vendor project.group ?: project.name
        }
        security {
            'all-permissions'()
        }

        signJarParams = [keystore: "${keystoreFile}", alias: "${demoKeyAlias}", storepass: "${keystorePassword}"]
    }
}