
subprojects {
    
    repositories {
        jcenter()
    }
    
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId "weblaf-$project.name"
                from components.java
                artifact sourceJar {
                    classifier 'sources'
                }
                if (project.ext.description) {
                    pom.withXml {
                        asNode().appendNode('description',project.ext.description)
                    }
                }

            }
        }
    }

    task fixJavaResources() {
        description = 'Moves resources from the java source folder into the src/main/resources directory.'
        File resources = file('src/main/resources')
        File sources = file('src/main/java')
        ant.copy(todir:"$resources") {
            fileset(dir:"$sources") {
                exclude(name:"**/*.java")
            }
        }
    }
}
